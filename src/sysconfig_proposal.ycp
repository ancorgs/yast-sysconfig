/**
 * File:	clients/sysconfig_proposal.ycp
 * Package:	Configuration of sysconfig
 * Summary:	Proposal function dispatcher.
 * Authors:	Ladislav Slezak <lslezak@suse.cz>
 *
 * $Id$
 *
 * Proposal function dispatcher for sysconfig configuration.
 */

{

textdomain "sysconfig";

import "Sysconfig";
import "Progress";

/* The main () */
y2milestone("----------------------------------------");
y2milestone("Sysconfig proposal started");

string func = WFM::Args(0);
map param = WFM::Args(1);
map ret = $[];

/* create a textual proposal */
if(func == "MakeProposal") {
    string proposal = "";
    string warning = nil;
    symbol warning_level = nil;
    boolean force_reset = param["force_reset"]:false;

    if(force_reset || !Sysconfig::proposal_valid) {
        Sysconfig::proposal_valid = true;
	Progress::off();
	Sysconfig::Read();
    }
    list sum = Sysconfig::Summary(false);
    proposal = select(sum, 0, "");
    Progress::on();

    ret = $[
	"preformatted_proposal" : proposal,
	"warning_level" : warning_level,
	"warning" : warning,
    ];
}
/* run the module */
else if(func == "AskUser") {
    map stored = Sysconfig::Export();
    symbol seq = WFM::CallFunction(`sysconfig(.propose));
    if(seq != `next) Sysconfig::Import(stored);
    y2debug("stored=%1",stored);
    y2debug("seq=%1",seq);
    ret = $[
	"workflow_sequence" : seq
    ];
}
/* create titles */
else if(func == "Description") {
    ret = $[
	"rich_text_title" : _("Sysconfig"),
	"menu_title" : _("&Sysconfig"),
	"id" : "sysconfig",
    ];
}
/* write the proposal */
else if(func == "Write") {
    Sysconfig::Write();
}
/* unknown function */
else {
    y2error("unknown function: %1", func);
}

/* Finish */
y2debug("ret=%1",ret);
y2milestone("Sysconfig proposal finished");
y2milestone("----------------------------------------");
return ret;

/* EOF */
}
