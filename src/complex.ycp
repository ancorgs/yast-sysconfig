/**
 * File:	include/sysconfig/complex.ycp
 * Package:	Configuration of sysconfig
 * Summary:	Dialogs definitions
 * Authors:	Ladislav Slezak <lslezak@suse.cz>
 *
 * $Id$
 */

{

textdomain "sysconfig";

import "Wizard";

import "Sysconfig";

include "ui/common_popups.ycp";
include "ui/common_messages.ycp";

include "sysconfig/helps.ycp";
include "sysconfig/routines.ycp";

/**
 * Return a modification status
 * @return true if data was modified
 */
global define boolean Modified() ``{
    return Sysconfig::Modified();
}


/**
 * Read settings dialog
 * @return `abort if aborted and `next otherwise
 */
global define symbol ReadDialog() ``{
    Wizard::RestoreHelp(HELPS["read"]:"");
    Sysconfig::AbortFunction = ``{ return PollAbort();};

// FIXME:    boolean ret = Sysconfig::Read(["/etc/sysconfig/*"]);
    boolean ret = Sysconfig::Read(["/local/lslezak/fillup-templates/*"]);	// TODO: add metadata file with predefined paths and descriptions

    return ret ? `next : `abort;
}

/**
 * Write settings dialog
 * @return `abort if aborted and `next otherwise
 */
global define symbol WriteDialog() ``{
    Wizard::RestoreHelp(HELPS["write"]:"");
    Sysconfig::AbortFunction = ``{ return PollAbort();};
    boolean ret = Sysconfig::Write();
    return ret ? `next : `abort;
}

global define string create_richtext_description(map description) ``{
    string varname = description["name"]:"";
    string file = description["file"]:"";
    string default_value = description["Default"]:"";
    string comment = description["comment"]:"";

    string possible_vals = possible_values(description);

    string result = "";

    if (file != "" && file != nil)
    {
	result = result + "<P><B>" + _("File: ") + "</B> " + file + "</P>";
    }

    if (possible_vals != "" && possible_vals != nil)
    {
	result = result + "<P><B>" + _("Possible values: ") + "</B> " + possible_vals + "</P>";
    }

    if (default_value != "" && default_value != nil)
    {
	result = result + "<P><B>" + _("Default value: ") + "</B> " + default_value + "</P>";
    }

    if (comment != "" && comment != nil)
    {
	result = result + "<P><B>" + _("Description: ") + "</B><BR> " + comment + "</P>";
    }

    return result;
}

global define boolean combo_editable(map description) ``{
    string type = description["Type"]:"";

    return (type == "" || regexpmatch(type, "^integer\\(.*:.*\\)$") || type == "integer"
	|| type == "string" || regexpmatch(type, "^string\\(.*\\)$") || type == "ip"
	|| regexpmatch(type, "^regexp\\(.*\\)$"));
}

global define list add_if_missing(list l, string v) ``{
    return (!contains(l, v)) ? add(l, v) : l;
}

global define list combo_list(map description) ``{
    string value = description["value"]:"";
    list ret = [];

    if (value != nil)
    {
	ret = [ value ];
    }

    string deflt = description["Default"]:"";
    string type = description["Type"]:"";

    if (type == "yesno")
    {
	if (!contains(ret, "yes")) ret = add(ret, "yes");
	if (!contains(ret, "no")) ret = add(ret, "no");
    }
    else if (type == "boolean")
    {
	if (!contains(ret, "true")) ret = add(ret, "true");
	if (!contains(ret, "false")) ret = add(ret, "false");
    }
    else if (regexpmatch(type, "^list\\(.*\\)"))
    {
	string values_string = regexpsub(type, "^list\\((.*)\\)", "\\1");

	list(string) values = splitstring(values_string, ",");

	foreach (string v, values, ``{
		if (!contains(ret, v)) ret = add(ret, v);
	    }
	);
    }
    else if (regexpmatch(type, "^string\\(.*\\)"))
    {
	string values_string = regexpsub(type, "^string\\((.*)\\)", "\\1");

	list(string) values = splitstring(values_string, ",");

	foreach (string v, values, ``{
		if (!contains(ret, v)) ret = add(ret, v);
	    }
	);
    }

    // add default value to the list
    if (!contains(ret, deflt)) ret = add(ret, deflt);

    return ret;
}

global define string possible_values(map description) ``{
    string ret = "";

    string type = description["Type"]:"";

    if (type == "yesno")
    {
	ret = ret + "yes,no";
    }
    else if (type == "boolean")
    {
	ret = "true,false";
    }
    else if (regexpmatch(type, "^list\\(.*\\)"))
    {
	ret = regexpsub(type, "^list\\((.*)\\)", "\\1");
    }
    else if (regexpmatch(type, "^string\\(.*\\)"))
    {
	ret = regexpsub(type, "^string\\((.*)\\)", "\\1");
	ret = ret +  " <I>" + _("or any value") + "</I>";
    }
    else if (regexpmatch(type, "^regexp\\(.*\\)"))
    {
	string regex = regexpsub(type, "^regexp\\((.*)\\)", "\\1");
	ret = sformat(_("<I>Value matching regular expression</I> %1"), regex);
    }
    else if (type == "integer")
    {
	ret = "<I>" + _("Any integer value") + "</I>";
    }
    else if (regexpmatch(type, "^integer\\(.*:.*\\)"))
    {
	string min = regexpsub(type, "^integer\\((.*):.*\\)", "\\1");
	string max = regexpsub(type, "^integer\\(.*:(.*)\\)", "\\1");

	if (max == nil && min != nil)
	{
	    ret = "<I>" + sformat(_("Integer value greater or equal to %1"), min) + "</I>";
	}
	else if (min == nil && max != nil)
	{
	    ret = "<I>" + sformat(_("Integer value less or equal to %1"), max) + "</I>";
	}
	else
	{
	    // Translation: %1 is minimum value, %2 is maximum integer value
	    ret = "<I>" + sformat(_("Any integer value from %1 to %2"), min, max) + "</I>";
	}
    }
    else if (type == "string")
    {
	ret = "<I>" + _("Any value") + "</I>";
    }

    return ret;
}

global define void update_combo(map description) ``{
    string varname = description["name"]:"";

    if (combo_editable(description))
    {
	UI::ReplaceWidget(`id(`replace), `ComboBox (`id(`combo), `opt(`editable, `hstretch), _("&Setting of: ") + varname,  combo_list(description)));
    }
    else
    {
	UI::ReplaceWidget(`id(`replace), `ComboBox (`id(`combo), `opt(`hstretch), _("&Setting of: ") + varname,  combo_list(description)));
    }

    // disable combo for non-leaf nodes
    UI::ChangeWidget(`id(`combo), `Enabled, ((description["file"]:"") != ""));
}

global define void update_button_state(map description) ``{
    string def = description["Default"]:nil;

    UI::ChangeWidget(`id(`def), `Enabled, (def != nil));
}

global define void update_location(map description) ``{
    string l = description["location"]:"";

    UI::ChangeWidget(`id(`heading), `Value, _("Current selection: ") + l);
}

global define boolean is_node(string id) ``{
    return (findfirstof(id, "$") == nil);
}

/**
 * Summary dialog
 * @return dialog result
 */
global define symbol SummaryDialog() ``{

    UI::OpenDialog(`opt(`defaultsize),
	`VBox(`Left(`Image(`suseheader, "SuSE")),
	    `HSpacing(85),
	    `HWeight(70,
		`VBox(
		    `HBox(
			`HWeight(35,
			    `Tree(`id(`tree), `opt(`notify, `vstretch), _("&Configuration options"), Sysconfig::tree_content)
			),
			`HSpacing(1),
			`HWeight(65,
			    `VBox(`HSpacing(60),
				`Left(`Heading(`id(`heading), `opt(`hstretch), "Current selection: ")),
				`VSpacing(0.5),
				`HBox(
				    `ReplacePoint(`id(`replace),
					`ComboBox (`id(`combo), `opt(`disabled, `hstretch), _("&Setting of: "),  [""])
				    ),
				    `VBox(
					`Label(""),
					`PushButton(`id(`def), `opt(`disabled), _("&Default"))
				    )
				),
				`VSpacing(1),
				`RichText(`id(`rt), _("<P><B>System configuration editor</B></P><P>With system configuration editor you can...</P>") +
				    _("<P><B>Note:</B> Descriptions are not translated</P>")
				)
			    )
			)
		    ),

		   `HBox(
			 // back pushbutton: the user input is ignored and the last dialog is called
			 `PushButton(`id(`back), AbortButtonLabel()),
			 `HStretch(),
			 `PushButton(`id(`help), HelpButtonLabel()),
			 `HStretch(),
			  // Translation: push button label
			 `PushButton(`id(`search), _("&Search")),
			 `HStretch(),
			 `PushButton(`id(`next), FinishButtonLabel())
			 )
		    )
		)
	    )
    );


    symbol ret = nil;

    while(ret != `cancel && ret != `abort && ret != `next && ret != `back)
    {
	ret = UI::UserInput();

	if (ret == `tree)
	{
	    string selected = UI::QueryWidget(`id(`tree), `CurrentItem);
	    y2milestone("Selected: %1", selected);

	    map description = Sysconfig::get_description(selected);

	    y2milestone("Descr: %1", description);

	    // update richtext content
	    UI::ChangeWidget(`id(`rt), `Value, create_richtext_description(description));

	    // update combo box
	    update_combo(description);

	    // update "Default" button state (enable/disable)
	    update_button_state(description);

	    // update location in header
	    update_location(description);
	}
	else if (ret == `abort || ret == `cancel)
	{
	    if (!ReallyAbort())
	    {
		ret = nil;
	    }
	    else
	    {
		// `cancel is same as `abort
		ret = `abort;
	    }
	}
    }

    UI::CloseDialog();

    return ret;
}

}
