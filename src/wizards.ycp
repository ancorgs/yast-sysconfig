/**
 * File:	include/sysconfig/wizards.ycp
 * Package:	Configuration of sysconfig
 * Summary:	Wizards definitions
 * Authors:	Ladislav Slezak <lslezak@suse.cz>
 *
 * $Id$
 */

{

textdomain "sysconfig";

import "Wizard";
import "Sysconfig";

import "Popup";
import "Label";
include "wizard/sequencer.ycp";

include "sysconfig/complex.ycp";
include "sysconfig/dialogs.ycp";

/**
 * Whole configuration of sysconfig
 * @return sequence result
 */
global define boolean SysconfigSequence() ``{

    map aliases = $[
	"read"	: [ ``( ReadDialog() ), true ],
	"main"	:   ``( MainSequence() ),
	"write"	: [ ``( WriteDialog() ), true ]
    ];

    map sequence = $[
	"ws_start" : "read",
	"read" : $[
	    `abort	: `abort,
	    `next	: "main"
	],
	"main" : $[
	    `abort	: `abort,
	    `next	: "write"
	],
	"write" : $[
	    `abort	: `abort,
	    `next	: `next
	]
    ];

    Wizard::CreateDialog();

    any ret = WizardSequencer(aliases, sequence);

    UI::CloseDialog();

    return ret == `next;
}

/**
 * Whole configuration of sysconfig but without reading and writing.
 * For use with autoinstallation.
 * @return sequence result
 */
global define symbol SysconfigAutoSequence() ``{
    Wizard::CreateDialog();

    any ret = MainSequence();

    UI::CloseDialog();
    return ret;
}

/**
 * Main workflow of the sysconfig configuration
 * @return sequence result
 */
global define symbol MainSequence() ``{

    map aliases = $[
	"main"	:   ``(MainDialog())
    ];

    map sequence = $[
	"ws_start" : "main",
	"main" : $[
	    `abort	: `abort,
	    `next	: `next
	]
    ];

    any ret = WizardSequencer(aliases, sequence);

    return ret;
}


/* EOF */
}
